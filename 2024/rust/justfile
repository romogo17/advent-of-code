# Use a template to generate a package for a new day's puzzle and fetch the input
create day:
    cargo generate --path ./daily-template --name {{day}}
    just get-input {{day}}

# Work on a day's puzzle part
work day part log_level="debug":
    cargo watch -w {{day}} -x "check --package {{day}}" -s "just test {{day}} {{part}} {{log_level}}"

# Run tests for a day's puzzle part
test day part log_level="info":
    RUST_LOG={{log_level}} cargo nextest run --package {{day}} {{part}}

# Run tests for a day's puzzle part in release mode
test-release day part log_level="info":
    RUST_LOG={{log_level}} cargo nextest run --package {{day}} {{part}} --release

# Run a day's puzzle part
run day part log_level="info":
    RUST_LOG={{log_level}} cargo run --package {{day}} --bin {{part}}

# Run a day's puzzle part in release mode
run-release day part log_level="info":
    RUST_LOG={{log_level}} cargo run --package {{day}} --bin {{part}} --release

# You can find SESSION by using Chrome tools:
# 1) Go to https://adventofcode.com/2024/day/1/input
# 2) right-click -> inspect -> click the "Application" tab.
# 3) Refresh
# 5) Click https://adventofcode.com under "Cookies"
# 6) Grab the value for session. Fill it into your .env file
# 
# example .env:
#
# ```
# SESSION=PASTE_COOKIE_VALUE_HERE
# ```
#
# Fetch the input for a day's puzzle
get-input day:
    ./scripts/get-aoc-input.rs --day {{day}} --current-working-directory {{justfile_directory()}}